Dn?=8;
k=TableLookup(ScrewDIN, Dn, Dn, k5);
s=TableLookup(ScrewDIN, Dn, Dn, s3);
P=TableLookup(ScrewDIN, Dn, Dn, P);
dh=TableLookup(ScrewDIN, Dn, Dn, dh);
e1=TableLookup(MetricThread, P, P, e1); # Grundlochüberhang
hk=0.5*P;
L=24;
deg60=45*deg;
tan60=tan(deg60);
Lt=k+(0.5*Dn)/tan60;
Rh=tan60*Lt;

axis=RefAxis(O, -EX);

mat=loadmodel(material_props);

screw:
( 
 Cylinder(O+k*EX, O+(L)*EX, Dn)
|
Revolution(Tri(O, 
 Lt*EX,
 Rh*EY
), O, EX, 2.*M_PI)
)
- Extrusion(
 RegPoly(O,EX,6,s/2.),
 k*EX)
- Revolution(
 Tri(O+(L)*EX+0.5*Dn*EY, -P*EY, -P*EX),
 O, EX, 2.*M_PI)
;
screw->density=mat$rho_steel;

Lthr?=L;
throughhole=
Cylinder(O, O+(Lthr+k)*EX, dh);

Lhh?=L;
Lhhb?=L;
headhole=
Cylinder(O, O+(Lhhb+k)*EX, dh)
|
Revolution(Tri(O, 
 EX*Lt,
 Rh*EY
), O, EX, 2.*M_PI)
|
Cylinder(O-Lhh*EX, O, 2.*Rh)
;

headhole_conical=
Cylinder(O, O+(Lhhb+k)*EX, dh)
|
Revolution(Tri(EX*Lt, 
 -EX*Lhh,
 (Lhh/tan60)*(EY-EX)
), O, EX, 2.*M_PI)
;

dbl=Dn-P;
blindhole=
Place(
 loadmodel(drill_hole, D=dbl, L=(L+e1), ofsTimesHtip=1, ofsTimesL=1), 
 O, -EX, EZ)
|
Revolution(Tri(O, 
 Lt*EX,
 Rh*EY
), O, EX, 2.*M_PI)
|
Cylinder(O-Lhh*EX, O, 2.*Rh)
;
